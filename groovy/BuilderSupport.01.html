<!DOCTYPE html PUBLIC "-//W3C//DTD HTML 4.01 Transitional//EN" "http://www.w3.org/TR/html4/loose.dtd">
<html>
<head>
    <meta http-equiv="Content-Type" content="text/html; charset=UTF-8">
    <link href="../css/shCore.css" type="text/css" rel="stylesheet" />
    <link href="../css/shThemeDefault.css" type="text/css" rel="stylesheet" />
    <link type="text/css" href="../css/style.css" rel="stylesheet"/>
    <script type="text/javascript" src="../js/jquery-1.3.2.min.js" ></script>
    <script type="text/javascript" src="../js/shCore.js" ></script>
    <script type="text/javascript" src="../js/shLegacy.js"></script>
    <script type="text/javascript" src="../js/shBrushJScript.js" ></script>
    <script type="text/javascript" src="../js/shBrushSql.js" ></script>
    <script type="text/javascript" src="../js/shBrushGroovy.js" ></script>
    <script type="text/javascript" src="../js/shBrushJava.js" ></script>
    <title>BuilderSupport - 01</title>
</head>
<body>
<div>
    <h1 class="header">BuilderSupport - 01</h1>
</div>
<div class="articlecontent">
    <h2>Preface</h2>
    <p>
    builder是groovy相當有趣(我不想用強大，因為對軟體開發技術高超的高手而言，井底之蛙如我覺得強大可能只是稀鬆平常的東西)的一個功能。
    除了Groovy網站有很多不同的builder，提供很方便有趣的功能之外，我們也可以繼承BuilderSupport這個abstrat class。
    這個class放在groovy.util這個package下，有興趣的可以去下載groovy source code來看。
    </p>
    <p>
    BuilderSupport.java裡面有一些method讓我們實作自己的builder專屬的功能，如下兩段截錄method definition
    </p>
    <h3>Listing. 部分 BuilderSupport.java</h3>
    <pre name="code" class="java:controls:firstline[151]">
    /**
     * A strategy method to allow derived builders to use
     * builder-trees and switch in different kinds of builders.
     * This method should call the setDelegate() method on the closure
     * which by default passes in this but if node is-a builder
     * we could pass that in instead (or do something wacky too)
     *
     * @param closure the closure on which to call setDelegate()
     * @param node    the node value that we've just created, which could be
     *                a builder
     */
    protected void setClosureDelegate(Closure closure, Object node) {
        closure.setDelegate(this);
    }
    
    protected abstract void setParent(Object parent, Object child);

    protected abstract Object createNode(Object name);

    protected abstract Object createNode(Object name, Object value);

    protected abstract Object createNode(Object name, Map attributes);

    protected abstract Object createNode(Object name, Map attributes, Object value);
    </pre>
    <h3>Listing. 部分 BuilderSupport.java cont.</h3>
    <pre name="code" class="java:controls:firstline[191]">
    /**
     * A hook to allow nodes to be processed once they have had all of their
     * children applied.
     *
     * @param node   the current node being processed
     * @param parent the parent of the node being processed
     */
    protected void nodeCompleted(Object parent, Object node) {
    }

    /**
     * A hook to allow nodes to be processed once they have had all of their
     * children applied and allows the actual node object that represents
     * the Markup element to be changed
     *
     * @param node   the current node being processed
     * @param parent the parent of the node being processed
     * @return the node, possibly new, that represents the markup element
     */
    protected Object postNodeCompletion(Object parent, Object node) {
        return node;
    }

    protected Object getCurrent() {
        return current;
    }

    protected void setCurrent(Object current) {
        this.current = current;
    }
    </pre>
    <p>
    而BuilderSupport.java運作基礎則是在下列程式片段，這邊我少剪一段source code，從source 
    code可以看到default constructor的proxyBuilder是自己。對我這個沒啥軟體開發技術的人而言，
    這個東西也是現成的、很棒的Template Method的使用。透過其他的constructor可以把builder的運作delegate
    給別的物件完成。
    </p>
    <pre name="code" class="java:controls:firstline[52]">
    /**
     * Convenience method when no arguments are required
     *
     * @param methodName the name of the method to invoke
     * @return the result of the call
     */
    public Object invokeMethod(String methodName) {
        return invokeMethod(methodName, null);
    }

    public Object invokeMethod(String methodName, Object args) {
        Object name = getName(methodName);
        return doInvokeMethod(methodName, name, args);
    }

    protected Object doInvokeMethod(String methodName, Object name, Object args) {
        Object node = null;
        Closure closure = null;
        List list = InvokerHelper.asList(args);

        //System.out.println("Called invokeMethod with name: " + name + " arguments: " + list);

        switch (list.size()) {
            case 0:
                node = proxyBuilder.createNode(name);
                break;
            case 1: {
                Object object = list.get(0);
                if (object instanceof Map) {
                    node = proxyBuilder.createNode(name, (Map) object);
                } else if (object instanceof Closure) {
                    closure = (Closure) object;
                    node = proxyBuilder.createNode(name);
                } else {
                    node = proxyBuilder.createNode(name, object);
                }
            }
            break;
            case 2: {
                Object object1 = list.get(0);
                Object object2 = list.get(1);
                if (object1 instanceof Map) {
                    if (object2 instanceof Closure) {
                        closure = (Closure) object2;
                        node = proxyBuilder.createNode(name, (Map) object1);
                    } else {
                        node = proxyBuilder.createNode(name, (Map) object1, object2);
                    }
                } else {
                    if (object2 instanceof Closure) {
                        closure = (Closure) object2;
                        node = proxyBuilder.createNode(name, object1);
                    } else if (object2 instanceof Map) {
                        node = proxyBuilder.createNode(name, (Map) object2, object1);
                    } else {
                        throw new MissingMethodException(name.toString(), getClass(), list.toArray(), false);
                    }
                }
            }
            break;
            case 3: {
                Object arg0 = list.get(0);
                Object arg1 = list.get(1);
                Object arg2 = list.get(2);
                if (arg0 instanceof Map && arg2 instanceof Closure) {
                    closure = (Closure) arg2;
                    node = proxyBuilder.createNode(name, (Map) arg0, arg1);
                } else if (arg1 instanceof Map && arg2 instanceof Closure) {
                    closure = (Closure) arg2;
                    node = proxyBuilder.createNode(name, (Map) arg1, arg0);
                } else {
                    throw new MissingMethodException(name.toString(), getClass(), list.toArray(), false);
                }
            }
            break;
            default: {
                throw new MissingMethodException(name.toString(), getClass(), list.toArray(), false);
            }

        }

        if (current != null) {
            proxyBuilder.setParent(current, node);
        }

        if (closure != null) {
            // push new node on stack
            Object oldCurrent = getCurrent();
            setCurrent(node);
            // let's register the builder as the delegate
            setClosureDelegate(closure, node);
            closure.call();
            setCurrent(oldCurrent);
        }

        proxyBuilder.nodeCompleted(current, node);
        return proxyBuilder.postNodeCompletion(current, node);
    }
    </pre>
    <h2>Environment</h2>
    <div>
        <ul>
            <li>Groovy Version: 1.7.0 JVM: 1.6.0_16</li>
        </ul>
    </div>
    <h2>書本上的範例</h2>
    <p>下面是書本上的範例，在這個case執行是沒有問題的。接著在踹builder的行為的時候，針對某些狀況有修改這個範例。</p>
    <h3>Listing. DebugBuilder from book</h3>
    <pre name="code" class="groovy:controls:firstline[1]">
class DebugBuilder extends BuilderSupport {
    def result = ''<<''
    def indent = ' ' * 4
    int indentCount = -1
    
    def createNode( name ) {
        indentCount++
        return check( name )
    }
    
    def createNode( name, value ) {
        return check( createNode( name ) << format( value ) )
    }
    
    def createNode( name, Map attributes ) {
        return check( createNode( name ) << format( attributes ) )
    }
    
    def createNode( name, Map attributes, value ) {
        return check( createNode( name, attributes ) << format( value ) )
    }
    
    void setParent( parent, child ) {
        result << "\n" << indent * indentCount << child.toString()
    }
    
    void nodeCompleted( parent, node ) {
        indentCount--
    }
    
    private check( descr ) {
        if( !current ) { 
            result << descr
        }
        return descr
    }
    
    private format( value ) {
        return '(' << value.toString() << ')'
    }
    
    private format( Map attributes ) {
        StringBuffer formatted = '' << '['
        attributes.each { key, value ->
            formatted << key << ':' << value << ', '
        }
        formatted.length = formatted.size() - 2
        formatted << ']'
        return formatted
    }
}

def builder = new DebugBuilder()

builder.foo() {
    bar()
    baz( 'x' ) {
        map( a:1 )
    }
}

assert "\n" + builder.result == '''
foo
    bar
    baz(x)
        map[a:1]'''

println builder.result

    </pre>
    <p>
    以下面這個程式為例，就會得到預期之外的結果
    </p>
    <pre name="code" class="groovy:controls:firstline[53]">
def builder = new DebugBuilder()

builder.foo( 'a' )

println builder.result

assert "\n" + builder.result == '''
foo(a)'''

    </pre>
    <h3>Listing. 修改後的DebugBuilder程式，並且放一些log</h3>
    <pre name="code" class="groovy:controls:firstline[1]">
class DebugBuilder extends BuilderSupport {
    def result = ''<<''
    def indent = ' ' * 4
    int indentCount = -1
    
    def createNode( name ) {
        println "createNode(name) name: ${name}"
        println "createNode(name) current: ${current}"
        indentCount++
        return check( name )
    }
    
    def createNode( name, value ) {
        println "createNode(name, value) name: ${name}"
        println "createNode(name, value) current: ${current}"
        if( !current )
            return createNode( name ) << check( format( value ) )
        else
            return check( createNode( name ) << format( value ) )
    }
    
    def createNode( name, Map attributes ) {
        println "createNode(name, attributes) name: ${name}"
        println "createNode(name, attributes) current: ${current}"
        if( !current ) 
            return createNode( name ) << check( format( attributes ) )
        else
            return check( createNode( name ) << format( attributes ) )
    }
    
    def createNode( name, Map attributes, value ) {
        println "createNode(name, attributes, value) name: ${name}"
        println "createNode(name, attributes, value) current: ${current}"
        if( !current )
            return createNode( name, attributes ) << check( format( value ) )
        else
            return check( createNode( name, attributes ) << format( value ) )
    }
    
    void setParent( parent, child ) {
        println "setParent called"
        result << "\n" << indent * indentCount << child.toString()
    }
    
    void nodeCompleted( parent, node ) {
        indentCount--
    }
    
    private check( descr ) {
        if( !current ) { 
            println "Write ${descr} to string buffer"
            result << descr
        }
        return descr
    }
    
    private format( value ) {
        return '(' << value.toString() << ')'
    }
    
    private format( Map attributes ) {
        StringBuffer formatted = '' << '['
        attributes.each { key, value ->
            formatted << key << ':' << value << ', '
        }
        formatted.length = formatted.size() - 2
        formatted << ']'
        return formatted
    }
}

def builder = new DebugBuilder()

builder.foo( 'b' ) {
    bar()
    baz( 'x' ) {
        map( a:1 )
    }
}

println builder.result

    </pre>
    <p>
    執行結果如下，雖然source code BuildSupport.java寫current看起來是current node。
    但是對英文不好的我而言，解釋起來比較像站在current node真正處理的node則是下面一層的node。
    比如說在foo( 'b' )這層的時候，除了invoke createNode( name, value )之外，current存的是null value
    ；當處理到bar的時候invoke createNode( name )時，current存放的是foo。相同的可以在baz( 'x' )的地方觀察到。
    </p>
    <pre class="console">
E:\lab\groovy>groovy DebugBuilder.01.groovy
createNode(name, value) name: foo
createNode(name, value) current: null
Write foo to string buffer
createNode(name) name: bar
createNode(name) current: foo
setParent called
createNode(name, value) name: baz
createNode(name, value) current: foo
createNode(name) name: baz
createNode(name) current: foo
setParent called
createNode(name, attributes) name: map
createNode(name, attributes) current: baz(x)
createNode(name) name: map
createNode(name) current: baz(x)
setParent called
foo(b)
    bar
    baz(x)
        map[a:1]
    </pre>
    <p>
    其實每個method被呼叫的狀況從BuilderSupport.java的doInvokeMethod()這個method就知道，總是想看看實際的狀況，
    而且也想知道current的意涵和功用。
    </p>
    <h2>Reference</h2>
    <div>
        <ul>
            <li>Groovy in Action, First Edition</li>
        </ul>
    </div>
</div>
<div>
<a rel="license" href="http://creativecommons.org/licenses/by/2.5/tw/"><img alt="Creative Commons License" src="http://i.creativecommons.org/l/by/3.0/88x31.png" border="0"></a>
本著作係採用<a rel="license" href="http://creativecommons.org/licenses/by/2.5/tw/">創用 CC 「姓名標示 2.5 台灣版」授權條款</a>釋出。
<!--/Creative Commons License-->

<!--
<rdf:RDF xmlns="http://web.resource.org/cc/"
 xmlns:dc="http://purl.org/dc/elements/1.1/"
 xmlns:rdf="http://www.w3.org/1999/02/22-rdf-syntax-ns#">
  <Work rdf:about="">
    <license rdf:resource="http://creativecommons.org/licenses/by/2.5/tw/" />
    <dc:title>[著作名稱]</dc:title>
    <dc:date>[著作權啟始年份]</dc:date>
    <dc:description>[著作描述]</dc:description>
    <dc:creator>
      <Agent>
        <dc:title>[作者]</dc:title>
      </Agent>
    </dc:creator>
    <dc:rights>
      <Agent>
        <dc:title>[著作權持有人]</dc:title>
      </Agent>
    </dc:rights>
    <dc:source rdf:resource="原始著作的網址。若非衍生著作則本行可刪除" />
  </Work>
  <License rdf:about="http://creativecommons.org/licenses/by/2.5/tw/">
    <permits rdf:resource="http://web.resource.org/cc/Reproduction"/>
    <permits rdf:resource="http://web.resource.org/cc/Distribution"/>
    <permits rdf:resource="http://web.resource.org/cc/DerivativeWorks"/>
    <requires rdf:resource="http://web.resource.org/cc/Notice"/>
    <requires rdf:resource="http://web.resource.org/cc/Attribution"/>
  </License>
</rdf:RDF>
-->
<span class="home"><a href="../index.html">Home</a></span>
</div>
<address>
Terence Chao, 2010/02/02
</address>
<script type="text/javascript">
window.onload = function () {
    dp.SyntaxHighlighter.HighlightAll('code');
    $( 'table tr:even' ).css( 'background-color', '#E5EECC' );
};
</script>
</body>
</html>
